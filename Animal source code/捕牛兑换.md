## record

```erlang
-record(exchange_item, {
         id = 0           %% 道具唯一id
        ,type = 1         %% 道具类型（游戏里面道具才用）
        ,treasure_num = 1 %% 夺宝期数
        ,sub_type = 1     %% 道具分类（0游戏道具，1自定义道具）
        ,price = 1        %% 总价格
        ,num = 1          %% 道具数量
        ,icon = ""        %% 道具图标
        ,name = ""        %% 道具名字
        ,desc = ""        %% 道具描述
        ,buy_type = 1     %% 兑换类型
        ,buy_num = 0      %% 兑换类型数量
        ,on_sale = 0      %% 是否上架
        ,condition = 0    %% 0没有，1特惠，2vip，3秒杀, 4限时, 5限购
        ,vip = 0          %% vip需求
        ,old_price = 0    %% 原价格
        ,start_time = 0   %% 开始时间
        ,end_time = 0     %% 结束时间
        ,stock = -1       %% 库存
        ,limit = 0        %% 每日限购
        ,list = []        %% 已经购买的列表
    }
).
```

exchange_item对应兑换商品信息。



## 兑换进程

exchange_mgr.erl是兑换进程。



### 进程状态

```erlang
-record(state, {
	list = []
}).
```

兑换进程的state record是一个list，用于存储兑换商品。



### 初始化

```erlang
init([]) ->
    ?INFO("[~w] 正在启动", [?MODULE]),
    process_flag(trap_exit, true),
    State = #state{},
    erlang:send_after(500, self(), reload),
    ?INFO("[~w] 启动完成", [?MODULE]),
    {ok, State}.
```



### reload消息

```erlang
handle_info(reload, State) ->
    reload(),
    {noreply, State};

%% 重载物品
reload() ->
    case db:get_all("select * from exchange_item") of
        {ok, []} -> ?MODULE ! delete_all;
        {ok, List} ->
            do_reload(List);
        _ ->
            ok
    end.

do_reload([]) -> ok;
do_reload([[Id, Type, Icon, Name, Desc, Num, Price, BuyType, BuyNum, OnSale, SubType, Condition, Vip, OldPrice, StartTime, EndTime, Stock, Limit] | L]) ->
    Item = #exchange_item{id = Id, type = mail_mgr:to_atom_assets(Type), icon = Icon, name = Name, desc = Desc, num = Num, price = Price, buy_type = mail_mgr:to_atom_assets(BuyType), buy_num = BuyNum, on_sale = OnSale, sub_type = SubType, condition = Condition, old_price = OldPrice, vip = Vip, start_time = StartTime, end_time = EndTime, stock = Stock, limit = Limit},
    ?MODULE ! {reload_item, Item},
    do_reload(L).
```

reload的处理很简单，读取数据库记录，接着存储到进程列表中。



### reload_item

```erlang
%% 重载物品 需要上架的
handle_info({reload_item, Item = #exchange_item{id = Id, on_sale = 1, condition = Condition, end_time = EndTime}}, State = #state{list = List}) ->
    NewList = lists:keystore(Id, #exchange_item.id, List, Item),
    case Condition of
        4 ->
            %% 如果是定时商品，需要在超时会下架
            cancel_timer(Id),
            Now = date:unixtime(),
            Ref = erlang:send_after(erlang:max(0, EndTime - Now) * 1000, self(), {delete_item, Id}),
            put({exchange, Id}, Ref);
        _ ->
            ok
    end,
    {noreply, State#state{list = NewList}};

%% 重载物品 需要下架的
handle_info({reload_item, _Item = #exchange_item{id = Id, on_sale = 0}}, State = #state{list = List}) ->
    cancel_timer(Id),
    NewList = lists:keydelete(Id, #exchange_item.id, List),
    {noreply, State#state{list = NewList}};
```

重载单个商品。





### get_item

```erlang
%% 获取单个物品信息
handle_call({get_item, Id}, _From, State = #state{list = List}) ->
    Reply = case lists:keyfind(Id, #exchange_item.id, List) of
        Item = #exchange_item{} ->
            {ok, Item};
        _ ->
            {false, ?error_treasure_item}
    end,
    {reply, Reply, State};
```





### exchange

```erlang
%% 兑换
handle_call({exchange, {_RoleId, Id, Num}}, _From, State = #state{list = List}) ->
        case lists:keyfind(Id, #exchange_item.id, List) of
            Item = #exchange_item{stock = Stock} ->
                %% 判断库存是否大于兑换数量
                case Stock >= Num of
                    true ->
                        NewStock = Stock - Num,
                        NewList = case NewStock of
                            %% 库存为0下架：更新数据库、从进程列表删除
                            0 -> 
                                db:exec("update exchange_item set on_sale = 0,stock = 0 where id = ?", [Id]),
                                lists:keydelete(Id, #exchange_item.id, List);
                            %% 不为0则更新数据库，更新进程列表
                            _ ->
                                db:exec("update exchange_item set stock = ? where id = ?", [NewStock, Id]),
                                lists:keyreplace(Id, #exchange_item.id, List, Item#exchange_item{stock = NewStock})
                        end,
                        {reply, {ok}, State#state{list = NewList}};
                    _ ->
                        case Stock =:= -1 of
                            true ->
                                {reply, {ok}, State};
                            _ ->
                                Reply = {false, ?error_exchange_item_num},
                                {reply, Reply, State}
                        end
                end;
            _ ->
                Reply = {false, ?error_treasure_item},
                {reply, Reply, State}
        end;
```

执行兑换处理。

这里的逻辑很简单，更新商品库存，根据库存来决定是否下架。





## 兑换进程方法



### exchange/2

exchange/2用于兑换商品。

我们的逻辑应该加在这里，如果是兑换椰子积分，



```erlang
%% 兑换
exchange(Role = #role{role_id = RoleId, name = _Name, icon = _Icon, address = Address, vip = Vip, exchange_log = LogList}, Id) ->
    Num = 1,
    %% 从进程中获取兑换商品
    case gen_server:call(?MODULE, {get_item, Id}) of
        {ok, Item = #exchange_item{buy_num = BuyPrice, buy_type = BuyType, sub_type = SubType, vip = Vip1, num = ItemNum, condition = Condition, start_time = StartTime, end_time = EndTime, name = ItemName, limit = Limit}} ->
            %% 为了方面看源码，我这里删除了一些不重要逻辑判断
            %% 扣除玩家资产
            case role_lib:do_cost(Role, [{BuyType, BuyPrice * Num}]) of
                {ok, NewRole} ->
                    %% 向进程发送exchange消息
                    case gen_server:call(?MODULE, {exchange, {RoleId, Id, Num}}) of
                        {ok} ->
                            Status = case SubType of
                                         0 -> 1;
                                         _ -> 0
                                     end,
                            case do_reward(Item, NewRole) of
                                {ok, NewRole0} ->
                                    role_lib:send_buff_flush(),
                                    NewRole1 = role_lib:set_value(NewRole0, {?daily_exchange, Id}, N + 1),
                                    %% mysql插入兑换记录
                                    log_db:log(exchange_log, insert, [RoleId, Id, ItemName, ItemNum, mail_mgr:to_integer_assets(BuyType), BuyPrice, Now, Status, Add, PName, Phone]),
                                    %% 存储玩家数据
                                    Log = #p_exchange_log{id = Id, name = ItemName, time = Now},
                                    NewLogList = lists:sublist([Log | LogList], 20),
                                    {ok, NewRole1#role{exchange_log = NewLogList}};
                                {false, Reason} ->
                                    role_lib:send_buff_clean(),
                                    {false, Reason}
                            end;
                        {false, Reason} ->
                            role_lib:send_buff_clean(),
                            {false, Reason};
                        _ ->
                            role_lib:send_buff_clean(),
                            {false, ?error_busy}
                    end;
                {false, Reason} ->
                    role_lib:send_buff_clean(),
                    {false, Reason}
            end;
        {false, Reason} ->
            {false, Reason};
        _ ->
            {false, ?error_busy}
    end.
```



以下是兑换处理

```erlang

%% 兑换成功发奖
do_reward(#exchange_item{sub_type = SubType, type = Type, num = ItemNum, price = Price0, name = ItemName}, Role = #role{role_id = RoleId, channel = Channel, exchange = Exchange}) ->
    Now = date:unixtime(),
    case SubType of
        %% 内部游戏道具处理
        0 ->
            case Type =:= jd_card of
                %% 京东卡处理
                true -> 
                    OrderID = lists:concat([jd, RoleId, Now]),
                    Price = util:ceil(Price0/ItemNum),
                    case lib_juhe:jd_card(Price, OrderID) of
                        {ok, Cami} ->
                            log_db:log(jd_card, insert, [RoleId, 5, 0, Cami, Price, Now]),
                            Msg = util:fbin("兑换成功!获得~w京东礼品卡:~n ~ts",[Price, Cami]),
                            mail_mgr:send(11, RoleId, "兑换成功", Msg, [], Now),
                            case Channel of
                                0 -> ok;
                                _ ->
                                    db:exec("insert into channel_exchange_log(channel_id, role_id, exchange, type, time) value(?, ?, ?, ?, ?)", [Channel, RoleId, Price0 * 100, 3, Now])
                            end,
                            {ok, Role#role{exchange = Exchange + Price0 * 100}};
                        _R -> 
                            {false, ?error_busy}
                    end;
                _ ->
                    Items = [#p_assets{type = Type, num = ItemNum}],
                    Msg = util:fbin("兑换成功!获得[~ts],请点击领取!",[ItemName]),
                    mail_mgr:send(11, RoleId, "兑换成功", Msg, Items, Now),
                    {ok, Role}
            end;
        %% 自定义商品处理
        1 ->
            Msg = util:fbin("兑换成功!获得[~ts], 等待客服发货",[ItemName]),
            mail_mgr:send(11, RoleId, "兑换成功", Msg, [], Now),
            case Channel of
                0 -> ok;
                _ ->
                    db:exec("insert into channel_exchange_log(channel_id, role_id, exchange, type, time) value(?, ?, ?, ?, ?)", [Channel, RoleId, Price0 * 100, 99, Now])
            end,
            {ok, Role#role{exchange = Exchange + Price0 * 100}}
    end.
```

参数1是兑换商品，参数2是玩家信息。







椰子积分的兑换要加在这里。

