## record

### treasure

```erlang
-record(treasure, {
         id = 0           %% 道具唯一id
        ,type = 1         %% 道具类型（游戏里面道具才用）
        ,treasure_num = 1 %% 夺宝期数
        ,sub_type = 1     %% 道具分类（0游戏道具，1自定义道具）
        ,price = 1        %% 总价格
        ,num = 1          %% 道具数量
        ,icon = ""        %% 道具图标
        ,name = ""        %% 道具名字
        ,desc = ""        %% 道具描述
        ,buy_price = 1    %% 单个价格，即购买金额
        ,buy_type = 1     %% 夺宝类型，即购买金额的类型
        ,buy_num = 0      %% 夺宝次数
        ,all_num = 0      %% 总次数
        ,now_treasure_num = 0  %% 当前夺宝编号
        ,list = []        %% 夺宝码
        ,role_list = []   %% 参与夺宝的人物信息
        ,on_sale = 0      %% 是否上架
        ,condition = 0     %% 0没有，1特惠，2vip，3秒杀, 4限时
        ,vip = 0          %% vip需求
        ,old_price = 0    %% 原价格
        ,start_time = 0   %% 开始时间
        ,end_time = 0     %% 结束时间
        ,stock = -1        %% 库存
    }
).
```

treasure对应每个商品，重点参数如下：

- sub_type

  分类

- price

  物品总价格

- num

  物品总数量

- buy_price

  兑换数量

- buy_type

  兑换类型

- buy_num

  夺宝次数

- all_num

  玩家可夺宝的次数

- now_treasure_num

  当前夺宝编号，该字段等于 商品ID * 100000

- list

  list是列表，存储着每个玩家对应的夺宝码。元素是{Number, RoleId}的元组。



```erlang
-record(treasure_log, {
        id = {0, 0}   %% 期数，类型
        ,time = 0     %% 开奖时间
        ,role_id = 0  %% 中奖id
        ,name = ""    %% 中奖名字
        ,icon = ""
        ,luck_num = 0 %% 幸运号码
    }
).
```

treasure_log record用于存储夺宝中奖数据。







```erlang
-record(role_treasure_log, {
        id = {0, 0}   %% 唯一id, 期数
        ,buy_time = 0 %% 购买时间
        ,time = 0     %% 开奖时间
        ,luck_num = 0 %% 幸运号码
        ,num_list = [] %% 夺宝码列表
    }
).
```





ets

- treasure表

  存储treasure记录。每个对象存储的是商品正在开奖的状态。

- treasure_log表

  存储treasure_log记录。即中奖记录。





## 协议

### 参与夺宝（1802）

```erlang
%% 参与夺宝
handle(1802, #m_1802_tos{id = Id, num = Num}, Role) ->
    case treasure_mgr:buy(Role, Id, Num) of
        {ok, NewRole, List} ->
            {ok, #m_1802_toc{list = List}, NewRole};
        {false, Reason} ->
            {false, Reason}
    end;
```





## 夺宝进程

treasure_mgr.erl文件是夺宝进程的实现。



### 进程状态

```erlang
-record(state, {
	list = []
}).
```

只有一个list字段，暂时还不知道用于存储什么？



### 初始化

```erlang
init([]) ->
    process_flag(trap_exit, true),
    
    %% 加载treasure dets表到ets中
    ets:new(treasure, [set, named_table, public, {read_concurrency, true}, {keypos, #treasure.id}]),
    dets:open_file(treasure, [{file, "./dets/treasure.dets"},  {keypos, #treasure.id}, {type, set}]),
    ets:from_dets(treasure, treasure),
    
    %% 加载treasure_log dets表到ets表中
    ets:new(treasure_log, [set, named_table, public, {read_concurrency, true}, {keypos, #treasure_log.id}]),
    dets:open_file(treasure_log, [{file, "./dets/treasure_log.dets"},  {keypos, #treasure_log.id}, {type, set}]),
    ets:from_dets(treasure_log, treasure_log),
    
    %% 1秒后向自己发送一个reload消息
    erlang:send_after(1000, self(), reload),
    
    State = #state{},
    {ok, State}.
```

treasure_mgr进程的初始化做了俩件事情，一是加载磁盘表数据到内存中，二是重载商品数据。



### reload消息

在服务启动的时候，重载商品数据。

```erlang
handle_info(reload, State) ->
    reload(),
    {noreply, State}.

reload() ->
    case db:get_all("select * from treasure_item") of
        {ok, List} ->
            do_reload(List);
        _ ->
            ok
	end.

%% do_reload参数是一个列表，每个元素即是一行记录。
%% Info是mysql表的一行记录，erlang以一个列表来存储一行记录
do_reload([]) -> ok;
do_reload([Info = [Id | _], L]) ->
    case get_item(Id) of
        {ok, T = #treasure{}} ->
            %% 处理原有商品，可能数据库表做了更新，需要同步到ets表中
            do_change(T, Info);
        _ ->
            %% 处理新的商品
            new_treasure(Info);
	end,
    do_reload(L).

%% 根据id从treasure表查询物品信息
get_item(Id) ->
    case ets:lookup(treasure, Id) of
        [T = #treasure{}] ->
            {ok, T};
        _ ->
            {false, ?error_treasure_item}
    end.
```

查询数据库的所有商品记录，同步到ets表中。

同步时有俩种情况：

- 已存在ets表中，需要同步ets表与mysql表，并添加到夺宝中
- 不存在ets表中，新增到ets表，再添加到夺宝中



以下代码是新增商品的处理逻辑。

```erlang
new_treasure([Id, Type, Icon, Name, Desc, Num, Price, BuyType, BuyNum, OnSale, SubType, Condition, Vip, OldPrice, StartTime, EndTime, Stock]) ->
    AllNum = get_all_num(Price, BuyType, BuyNum),

    %% 转换成treasure record，存储到ets表中
    Treasure = #treasure{id = Id, type = mail_mgr:to_atom_assets(Type), sub_type = SubType, price = Price, num = Num, icon = Icon, name = Name, desc = Desc, buy_price = BuyNum, buy_type = mail_mgr:to_atom_assets(BuyType), all_num = AllNum, now_treasure_num = init_treasure_num(Id), on_sale = OnSale, condition = Condition, vip = Vip, old_price = OldPrice, start_time = StartTime, end_time = EndTime, stock = Stock},
    ets:insert(treasure, Treasure),
    
    %% 如果物品商家，则向进程发送一条add_item消息
    case OnSale of
        1 ->
            ?MODULE ! {add_item, Treasure};
        _ ->
            ok
    end.
```



以下代码是ets表存在商品的处理逻辑：

```erlang
%% 从ets表取出的数据与mysql表的数据不一致，因此要进行同步，
do_change(T = #treasure{on_sale = _OnSale1}, [Id, Type, Icon, Name, Desc, Num, Price, BuyType, BuyNum, OnSale, SubType, Condition, Vip, OldPrice, StartTime, EndTime, Stock]) ->
    %% 计算该商品的夺宝总次数
    AllNum = get_all_num(Price, BuyType, BuyNum),
    
    %% 新的treasure
    Treasure = T#treasure{id = Id, type = mail_mgr:to_atom_assets(Type), sub_type = SubType, price = Price, num = Num, icon = Icon, name = Name, desc = Desc, buy_price = BuyNum, buy_type = mail_mgr:to_atom_assets(BuyType), all_num = AllNum, now_treasure_num = init_treasure_num(Id), on_sale = OnSale, condition = Condition, vip = Vip, old_price = OldPrice, start_time = StartTime, end_time = EndTime, stock = Stock},
    
    %% 如果上架就插入到ets表中
    case OnSale of
        1 ->
            %% Treasure插入到treasure ets表中，相当于覆盖了原有数据
            ets:insert(treasure, Treasure),
            ?MODULE ! {add_item, Treasure};
        _ ->
            ?MODULE ! {delete_item, Treasure}
    end.
```

do_change函数有俩个参数，参数1是ets表中的商品数据，参数2是mysql表中的商品数据。







### add_item消息

```erlang
%% 新上架物品
handle_info({add_item, Info = #treasure{id = Id, stock = Stock, condition = Condition, end_time = End}}, State = #state{list = List}) when Stock =/= 0->
    %% 将商品加入到进程列表中
    NewList = case lists:keyfind(Id, #treasure.id, List) of
        T = #treasure{} -> 
            Treasure = do_treasure_change(Info, T),
            lists:keyreplace(Id, #treasure.id, List, Treasure);
         _ ->
            [Info | List]
    end,
    
    %% 限时处理
    case Condition of
        4 -> 
            Now = date:unixtime(),
            case End > Now of
                true ->
                    Ref = erlang:send_after((End - Now) * 1000, self(), {open_time, Id}),
                    put({time_open, Id}, Ref);
                _ ->
                    erlang:send_after(1, self(), {open_time, Id})
            end;
        _ -> ok
    end,
    
    {noreply, State#state{list = NewList}};
```

add_item消息用于上架商品，即将商品添加夺宝进程列表中。





### open_time消息

```erlang
handle_info({open_time, Id}, State = #state{list = List}) ->
    %% 获取夺宝商品
    NewState = case lists:keyfind(Id, #treasure.id, List) of
        Treasure = #treasure{role_list = RoleList, list = TreasureList, treasure_num = TreasureNum, sub_type = SubType, buy_num = BuyNum, all_num = AllNum} when TreasureList =/= [] -> 
            %% 起始夺宝码
            Start = init_treasure_num(Id),
			%% 截至夺宝码
            End = Start + AllNum - 1,

            %% 随机产生一个夺宝码
            Num = sys_rand:rand(Start, End),
			
            %% 从夺宝码列表中寻找中奖玩家
            {RoleId, Name, Icon, {Add, PName, Phone}} = case lists:keyfind(Num, 1, TreasureList) of
				%% 如果有中奖玩家则返回该玩家信息，否则返回nil的玩家信息
                {Num, RoleId0} ->
                    case lists:keyfind(RoleId0, 1, RoleList) of
                        false -> {0, "", "", {"", "", ""}};
                        _Info -> _Info
                    end;
                _ -> {0, "", "", {"", "", ""}}
            end,

            %% 生成treasure_log record
            Log = #treasure_log{id = {TreasureNum, Id}, time = date:unixtime(), role_id = RoleId, name = Name, icon = Icon, luck_num = Num},

			%% Status，1是游戏道具，0非游戏道具
            Status = case SubType of
                0 -> 1;
                _ -> 0
            end,
            do_reward(Treasure, RoleId, TreasureNum, Num),
            log_db:log(treasure_reward_log, insert, [Id, TreasureNum, BuyNum, Num, RoleId, Status, Add,  PName, Phone, date:unixtime()]),
            ets:insert(treasure_log, Log),
            NewTreasure = Treasure#treasure{treasure_num = TreasureNum + 1, role_list = [], list = [], buy_num = 0, now_treasure_num = init_treasure_num(Id)},

            %% 由于是限时开奖，因此做下架处理
            ets:insert(treasure, NewTreasure#treasure{on_sale = 0}),             
            db:exec("update treasure_item set on_sale = 0 where id = ?", [Id]),
            NewList = lists:keydelete(Id, #treasure.id, List),
            State#state{list = NewList};
        _ ->
            State
    end,
    {noreply, NewState};
```

open_time消息用于处理限时开奖。





 



### delete_item消息

```erlang
%% 下架物品, 如果已经有人购买，开奖后删除
handle_info({delete_item, NewTreasure = #treasure{id = Id}}, State = #state{list = List}) ->
    %% 取消该商品对应的定时器，如果存在的话。
    cancel_timer(Id),
    
    NewState = case lists:keyfind(Id, #treasure.id, List) of
        Treasure = #treasure{all_num = AllNum, role_list = RoleList, list = TreasureList, treasure_num = TreasureNum, buy_num = BuyNum, sub_type = SubType} when TreasureList =/= []->
            
            Start = init_treasure_num(Id),
            End = Start + AllNum - 1,
            Num = sys_rand:rand(Start, End),

            {RoleId, Name, Icon, {Add, PName, Phone}} = case lists:keyfind(Num, 1, TreasureList) of
                {Num, RoleId0} ->
                    case lists:keyfind(RoleId0, 1, RoleList) of
                        false -> {0, "", "", {"", "", ""}};
                        _Info -> _Info
                    end;
                _ -> {0, "", "", {"", "", ""}}
            end,
            Log = #treasure_log{id = {TreasureNum, Id}, time = date:unixtime(), role_id = RoleId, name = Name, icon = Icon, luck_num = Num},
            Status = case SubType of
                0 -> 1;
                _ -> 0
            end,
            do_reward(Treasure, RoleId, TreasureNum, Num),
            log_db:log(treasure_reward_log, insert, [Id, TreasureNum, BuyNum, Num, RoleId, Status, Add,  PName, Phone, date:unixtime()]),
            ets:insert(treasure_log, Log),
            NewList = lists:keydelete(Id, #treasure.id, List),
            ets:insert(treasure, NewTreasure#treasure{treasure_num = TreasureNum + 1}),
            State#state{list = NewList};
        #treasure{treasure_num = TreasureNum} ->
            NewList = lists:keydelete(Id, #treasure.id, List),
            ets:insert(treasure, NewTreasure#treasure{treasure_num = TreasureNum}),
            State#state{list = NewList};
        _ ->
            State
    end,
    {noreply, NewState};

```

下架商品。





### buy消息

```erlang
handle_call({buy, {RoleId, Name, Icon, Id, Num, Add}}, _From, State = #state{list = List}) ->
    %% 从进程的list中寻找商品
    case lists:keyfind(Id, #treasure.id, List) of
       Treasure = #treasure{all_num = AllNum, buy_num = BuyNum, treasure_num = TreasureNum, now_treasure_num = NowNum, role_list = RoleList, list = TreasureList} ->
            %% 参与次数的判断
            case BuyNum + Num > AllNum of
                true ->
                    Reply = {false, ?error_treasure_num},
                    {reply, Reply, State};
                _ ->
                    %% 夺宝参与次数
                    NewBuyNum = BuyNum + Num,
                    
                    %% NowNum是夺宝码，玩家每参与一次就会分配一个夺宝码。
                    NewNowNum = NowNum + Num,
                    
                    %% 获取参与该夺宝商品的所有玩家
                    NewRoleList = case lists:keyfind(RoleId, 1, RoleList) of
                        false ->
                            [{RoleId, Name, Icon, Add} | RoleList];
                        _ ->
                            RoleList
                    end,
                    
                    %% 生成要分配给玩家的夺宝码
                    NumList = lists:seq(NowNum, NowNum + Num - 1),
                    List1 = [{_Num, RoleId} || _Num <- NumList],
                    NewTreasureList = List1 ++ TreasureList,
                    
                    %% 更新夺宝商品相关的字段
                    NewTreasure = Treasure#treasure{buy_num = NewBuyNum, now_treasure_num = NewNowNum, role_list = NewRoleList, list = NewTreasureList},
                    
                    %% 更新存储在进程列表中的夺宝商品
                    NewList = lists:keyreplace(Id, #treasure.id, List, NewTreasure),
                    
                    %% NumList：玩家参与的夺宝码列表，TreasureNum：夺宝期数
                    Reply = {ok, NumList, TreasureNum},
                    
                    %% 当参与次数 == 该商品最大的参与次数时，10秒后开奖
                    case NewBuyNum =:= AllNum of
                        true ->
                            erlang:send_after(10000, self(), {open_reward, Id});
                        _ ->
                            ok
                    end,
                    {reply, Reply, State#state{list = NewList}}
            end;
        _ ->
            Reply = {false, ?error_treasure_item},
            {reply, Reply, State}
    end;
```

- Id

  商品id

- Num

  购买数量

- Add

  一个{Add, PName, Phone}的元组，分别是地址、姓名、联系电话。



### open_reward消息

open_reward是开奖消息。

```erlang
%% 普通开奖
handle_info({open_reward, Id}, State = #state{list = List}) ->
    NewState = case lists:keyfind(Id, #treasure.id, List) of
        Treasure = #treasure{role_list = RoleList, list = TreasureList, treasure_num = TreasureNum, sub_type = SubType, buy_num = BuyNum, stock = Stock} when TreasureList =/= []->
            %% 从夺宝码列表中随机取出一个元素
            {Num, RoleId} = sys_rand:rand_list(TreasureList),
            
            %% 找到中奖的玩家
            {RoleId, Name, Icon, {Add, PName, Phone}} = lists:keyfind(RoleId, 1, RoleList), 
            %% 生成treasure_log record
            Log = #treasure_log{id = {TreasureNum, Id}, time = date:unixtime(), role_id = RoleId, name = Name, icon = Icon, luck_num = Num},
            
            %% 如果是游戏道具，Status=1，否则=0
            Status = case SubType of
                0 -> 1;
                _ -> 0
            end,
            do_reward(Treasure, RoleId, TreasureNum, Num),
            
            %% 插入夺宝中奖记录
            log_db:log(treasure_reward_log, insert, [Id, TreasureNum, BuyNum, Num, RoleId, Status, Add,  PName, Phone, date:unixtime()]),
            ets:insert(treasure_log, Log),

            %% 减库存
            NewStock = case Stock > 0 of
                true ->
                    Stock - 1;
                _ ->
                    Stock
            end,
            
            %% 期数+1，重置该夺宝商品的相关字段
            NewTreasure = Treasure#treasure{treasure_num = TreasureNum + 1, role_list = [], list = [], buy_num = 0, now_treasure_num = init_treasure_num(Id), stock = NewStock},
            NewList = case NewStock =:= 0 of
                %% 库存为0时下架
                true ->
                    ets:insert(treasure, NewTreasure#treasure{on_sale = 0}),
                    db:exec("update treasure_item set on_sale = 0,stock = 0 where id = ?", [Id]),
                    lists:keydelete(Id, #treasure.id, List);
                %% 库存不未0时的处理
                _ ->
                    ets:insert(treasure, NewTreasure),
                    case NewStock =:= -1 of
                        true -> ok;
                        _ ->
                            db:exec("update treasure_item set stock = ? where id = ?", [NewStock, Id])
                    end,
                    lists:keyreplace(Id, #treasure.id, List, NewTreasure)
            end,
            State#state{list = NewList};
        _ ->
            State
    end,
    {noreply, NewState};
```







### terminate

```erlang
terminate(_Reason, _State) ->
    ets:to_dets(treasure, treasure),
    ets:to_dets(treasure_log, treasure_log),
    ok.
```

当一个进程的销毁时，会将ets表的数据存储到dets表中。







## 夺宝进程方法

### init_treasure_num

```erlang
%% 初始化夺宝码
init_treasure_num(Type) ->
    Type * 100000.
```



### get_all_num

计算一个商品的总夺宝次数，即是玩家可以参数次数。

```erlang
get_all_num(Price, BuyType, BuyNum) ->
    %% 将兑换类型从int型转成原子型
    Type = mail_mgr:to_atom_assets(BuyType),
    Price1 = get_type_price(Type),
    
    util:ceil(Price/(Price1 * BuyNum)).

get_type_price(gold) -> 1/100;
get_type_price(coin) -> 1/10000;
get_type_price(red_bag) -> 1/100.
```

根据物品的总价格，玩家的兑换数量与兑换价格，计算出总夺宝次数。

- Price

  物品总价格，单位元。

- BuyType

  兑换类型

- BuyNum

  兑换数量，即兑换类型的货币要消耗多少数量。

在捕牛中，gold是兑换券，red_bag是红包，它们与RMB元的兑换比例是，1元等于100红包或兑换券。



### buy

buy/3是玩家参数夺宝商品。

```erlang
%% 购买夺宝码
buy(Role = #role{role_id = RoleId, name = Name, icon = Icon, treasure_log = List, address = Address, vip = Vip}, Id, Num) ->
    case get_item(Id) of
        {ok, #treasure{buy_price = BuyPrice, buy_type = BuyType, sub_type = SubType, condition = Condition, vip = Vip1, start_time = StartTime, end_time = EndTime}} ->
            Now = date:unixtime(),
            %% vip条件判断
            case  Vip >= Vip1 of
                true ->
                    %% 限时开奖判断
                    case Condition =/=4 orelse (Now >= StartTime andalso Now < EndTime) of
                        true ->
                            case check_address(SubType, Address) of
                                {_, Add, PName, Phone} ->
                                    role_lib:send_buff_begin(),
                                    %% 扣除玩家的货币
                                    case role_lib:do_cost(Role, [{BuyType, BuyPrice * Num}]) of
                                        {ok, NewRole} ->
                                            %% 向treasure_mgr进程发送buy消息
                                            case gen_server:call(?MODULE, {buy, {RoleId, Name, Icon, Id, Num, {Add, PName, Phone}}}) of
                                                {ok, NumList, TreasureNum} ->
                                                    %% 生成夺宝日志
                                                    Log = #role_treasure_log{id = {Id, TreasureNum}, buy_time = Now, num_list = NumList},
                                                    role_lib:send_buff_flush(),
                                                    log_db:log(role_treasure_log, insert, [Id, RoleId, TreasureNum, Num, util:to_string(NumList), Now]),
                                                    {ok, NewRole#role{treasure_log = lists:sublist([Log | List], 20)}, NumList};
                                                {false, Reason} ->
                                                    role_lib:send_buff_clean(),
                                                    {false, Reason};
                                                _ ->
                                                    role_lib:send_buff_clean(),
                                                    {false, ?error_busy}
                                            end;
                                        {false, Reason} ->
                                            role_lib:send_buff_clean(),
                                            {false, Reason}
                                    end;
                                _ ->
                                    {false, ?error_treasure_address}
                            end;
                        _ ->
                            {false, ?error_treasure_time}
                    end;
                _ ->
                    {false, ?error_vip}
            end;
        {false, Reason} ->
            {false, Reason}
    end.
```



- Id：商品id
- Num：购买数量

role record的treasure_log字段是一个列表，用于存储前20条夺宝日志，每条日志都是一个role_treasure_log record。









## problesm

删除商品怎么处理

整个模块的协议需要看下

log_db的运作原理需要查看下。 



bug：

如果一个限时商品参与人数满了，在开奖之后，该商品会继续上架，在商品到期时如果没有人参与，那么该商品会一直存在。





