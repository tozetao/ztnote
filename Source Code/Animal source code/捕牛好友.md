## 好友进程



### 初始化

```erlang
init([]) ->
    ?INFO("[~w] 正在启动", [?MODULE]),
    process_flag(trap_exit, true),
    do_new_ets(),
    do_new_dets(),
    
    %% 将dets表中的数据载入到ets表中
    dets_to_ets(),
    
    ?INFO("[~w] 启动完成", [?MODULE]),
    
    %% 明日0点触发消息
    erlang:send_after(date:next_diff(0, 0, 0) * 1000, self(), send_coin),
    %% 明日凌晨4点触发消息
    erlang:send_after(date:next_diff(4, 0, 0) * 1000, self(), save_dets),

    State = #state{},
    {ok, State}.
```



```erlang
do_new_ets(?friend_db_num) -> ok;
do_new_ets(N) ->
    Name = erlang:list_to_atom(lists:concat([friend_, N])),
    ets:new(Name, [set, named_table, public, {keypos, #friend.role_id}]),
    do_new_ets(N + 1).
```

创建存储玩家好友数据的ets表。



```erlang
do_new_dets(?friend_db_num) -> ok;
do_new_dets(N) ->
    Name = erlang:list_to_atom(lists:concat([friend_, N])),
    dets:open_file(Name, [{file, lists:concat(["./dets/", Name, ".dets"])}, {keypos, #friend.role_id}, {type, set}]),
    do_new_dets(N+1).
```

创建存储玩家好友数据的dets表。



### add_friend

```erlang
%% 成功邀请一位好友(新用户)
handle_info({add_friend, Id, Info}, State) ->
    add_friend1(Id, Info),
    {noreply, State};
```

add_friend消息的回调处理，这里add_friend1()系列的函数才是绑定好友的主体逻辑。

```erlang
%% 增加好友 一级
add_friend1(0, _Id) -> ok;
add_friend1(ParentId, {Id, Name, Icon}) ->
    %% 寻找玩家的好友数据
    case lookup(Id) of
        false ->
            %% 将该玩家的好友数据插入到ets表中
            save(#friend{role_id = Id, parent_id = ParentId, name = Name, icon = Icon}),
            %% 寻找上级玩家的好友数据
            case lookup(ParentId) of
                {ok, Friend = #friend{role_id = ParentId, parent_id = ParentId1, list1 = List, num1 = Num1, all_num = AllNum}} -> 
                    %% 存储一级好友关系
                    save(Friend#friend{role_id = ParentId, list1 = [Id | List], num1 = Num1 + 1, all_num = AllNum + 1}),
                    %% 绑定当前玩家的二级好友关系
                    add_friend2(ParentId1, Id);
                _ ->
                    %% 未找到则初始化该上级玩家的好友数据
                    save(#friend{role_id = ParentId, list1 = [Id], num1 = 1, all_num = 1})
            end;
        _ ->
            case lookup(ParentId) of
                {ok, Friend = #friend{role_id = ParentId, parent_id = ParentId1, list1 = List, num1 = Num1, all_num = AllNum}} -> 
                    save(Friend#friend{role_id = ParentId, list1 = [Id | List], num1 = Num1 + 1, all_num = AllNum + 1}),
                    add_friend2(ParentId1, Id);
                _ ->
                    save(#friend{role_id = ParentId, list1 = [Id], num1 = 1, all_num = 1})
            end
    end.
```

建立玩家与上级（一级）玩家的好友关系。



```erlang
%% 增加好友 二级
add_friend2(0, _Id) -> ok;
add_friend2(ParentId, Id) ->
    case lookup(ParentId) of
        {ok, Friend = #friend{role_id = ParentId, parent_id = ParentId1, list2 = List, num2 = Num1, all_num = AllNum}} -> 
            %% 将玩家与二级推荐人（上上级）进行绑定。
            save(Friend#friend{role_id = ParentId, list2 = [Id | List], num2 = Num1 + 1, all_num = AllNum + 1}),
            add_friend3(ParentId1, Id);
        _ ->
            ok
    end.
```

建立玩家与上级（二级，即父级的父级）玩家的好友关系。

注：如果能够在ets表中寻找到ParentId，就表示当前玩家是存在上级推荐人的。



三级与四级好友的建立与2级好友的建立是相同的逻辑，这里就省略代码了。



### charge

```erlang
handle_info({charge, Id, Rmb}, State) ->
    %% 获取分销配置
    Setting = get_friend_setting(),
    %% 进行返利
    add_charge(Id, Rmb, 1, Setting),
    {noreply, State};
```

新增上级好友的福利。

- Id：推荐人id
- Rmb：充值金额，单位分。



```erlang
%% 增加好友福利 处理四级
add_charge(0, _, _N, _) -> ok;
add_charge(_, _, 5, _) -> ok;
add_charge(Id, Rmb, N, Setting) ->
    %% 根据分销等级N获取收益的比例
    case lists:keyfind(N, 1, Setting) of
        {N, Per, 1} when Per > 0->
            Add = trunc(Rmb * 100 * Per /100),
            case catch lookup(Id) of
                {ok, Friend = #friend{parent_id = ParentId, coin = Coin, all_coin = AllCoin}} -> 
                    %% 修改内存中玩家的收益数据
                    NewFriend = Friend#friend{coin = Coin + Add, all_coin = AllCoin + Add},
                    save(NewFriend),
                    %% 处理上一级的分销玩家的分润
                    add_charge(ParentId, Rmb, N + 1, Setting);
                _ -> 
                    ok
            end;
        _ ->
            ok
    end.
```





### send_coin

每日凌晨会定时发放玩家的福利。

```erlang
handle_info(send_coin, State) ->
    send_coin(),
    erlang:send_after(86400 * 1000, self(), send_coin),
    {noreply, State};
```

处理send_coin的回调函数。



```erlang
send_coin(?friend_db_num) -> ok;
send_coin(N) ->
    Time = date:unixtime(),
    Name = erlang:list_to_atom(lists:concat([friend_, N])),
    Ms = ets:fun2ms(fun(Friend = #friend{coin = Coin}) when Coin >= 1000 -> 
                Friend
        end),
    List = ets:select(Name, Ms),
    do_send_coin(List, Time),
    send_coin(N + 1).
```

针对每张ets表，发送玩家的收益。



```erlang
do_send_coin([Friend = #friend{role_id = RoleID, coin = Coin} | L], Time) when Coin >= 100-> 
    %% 发送邮件
    send_coin_mail(RoleID, Coin, Time),
    %% 扣除金币，存储到内存中
    save(Friend#friend{coin = Coin rem 1000}),
    do_send_coin(L, Time);
do_send_coin([_ | L], Time) ->
    do_send_coin(L, Time);
do_send_coin([], _) -> ok.
```

对列表中玩家发送金币。



```erlang
%% 发送福利邮件
send_coin_mail(RoleID, Coin, Time) -> 
    Items = [#p_assets{type = red_bag, num = trunc(Coin/100)}],
    mail_mgr:send(0, RoleID, "好友福利", "您的好友慷慨华贵，一掷千金，奖励：", Items, Time).
```









## 好友进程方法

### add_friend

该函数用于新增下线好友，该函数会在首次注册时调用。

```erlang
%% 增加好友,注册就算 Info:{RoleId, Name, Icon}
add_friend(_, 0) ->    
    ok;
add_friend(Info, Id) ->    
    ?MODULE ! {add_friend, Id, Info}.
```

- Info：当前玩家信息，是一个{RoleId, Name, Icon}的元组。
- Id：父级好友的id



### charge

charge方法会为上级推荐人增加返利。

```erlang
charge(0, _) -> ok;
charge(Id, Rmb) ->
    ?MODULE ! {charge, Id, Rmb}.
```

- Id：推荐人玩家id
- Rmb：充值金额，单位分







## 关联record

```erlang
-record(friend, {
    role_id = 0,
    icon = "",
    name = "",
    parent_id = 0,
    list1 = [],
    list2 = [],
    list3 = [],
    list4 = [],
    num1 = 0,
    num2 = 0,
    num3 = 0,
    num4 = 0,
    all_icon = 0,	%% 好友总福利
    all_num = 0,    %% 总邀请好友数
    coin = 0,		%% 今日福利
    num = 0		%% 今日邀请好友数
}).
```

存储玩家好友数据的record。









