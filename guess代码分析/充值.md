## 协议

### 充值

```erlang
%% 充值接口
handle(1122, #m_1122_tos{type = 3}, _Role = #role{first_gift = 1}) ->
    {false, ?error_first_gift};
handle(1122, _, _Role = #role{role_id = 1129055}) ->
    {false, ?error_busy};
handle(1122, _, _Role = #role{role_id = 1033546}) ->
    {false, ?error_busy};
handle(1122, #m_1122_tos{type = Type, num = Num, charge_type = ChargeType, return_url = ReturnUrl}, Role) ->
    case charge:do_charge(Role, Type, Num, ChargeType, ReturnUrl) of
        {ok, Data} ->
            {reply, #m_1122_toc{info = Data, charge_type = ChargeType}};
        {false, Reason} ->
            {false, Reason}
    end;
```

充值协议是1122，位于role_rpc.erl模块中，它会去调用charge/do_charge/5函数。

参数说明：

- Num

  充值金额，单位分。

- Type

  充值类型，比如金币、钻石。

- ChargeType

  支付类型，可能是微信、paysapi、人工充值、网银支付。

- Role

  role记录，对应玩家进程的数据。role:handle_rpc(Pid, Mod, Cmd, Data, Flag)在调用handle(1122)充值接口时会传递该玩家进程的数据。



```erlang
%% 玩家充值
do_charge(_Role = #role{role_id = RoleId, pay_openid = OpenID, ip = Ip}, Type, Num, ChargeType, ReturnUrl) ->
    Now = date:unixtime(),
    N = sys_rand:rand(1000, 9999),
    Id = lists:concat([RoleId, N, Now]),
    case db:exec("insert into charge_log (id, role_id, charge_rmb, charge_type, type, time, status) values(?, ?, ?, ?, ?, ?, ?)", [Id, RoleId, Num, ChargeType, Type, date:unixtime(), ?charge_status_start]) of
        ok ->
            case ChargeType of
                ?charge_wxpubpay ->
                    wxpubpay(OpenID, Id, Num, RoleId, Type);
                ?charge_wft_wx ->
                    wft_gzh(gzh, OpenID, Id, Num, RoleId, Type);
                ?charge_wft_wx_sm ->
                    wft_gzh(sm, OpenID, Id, Num, RoleId, Type);
                ?charge_wft_zfb ->
                    wft_gzh(zfb, OpenID, Id, Num, RoleId, Type);
                ?charge_yb_wx_sm ->
                    yb_pay(ChargeType, OpenID, Id, Num, RoleId, Type);
                ?charge_yb_yl_sm_lx ->
                    yb_pay(ChargeType, OpenID, Id, Num, RoleId, Type);
                ?charge_yao_zfb ->
                    yao_zfb(zfb, Type, Num, Id, util:to_ip_string(Ip));
                ?charge_yao_wx ->
                    yao_zfb(wx, Type, Num, Id, util:to_ip_string(Ip));
                 ?charge_yao_wx2 ->
                     yao_zfb2(wx, Type, Num, Id, util:to_ip_string(Ip));
                 ?charge_waihui ->
                     Amt = float_to_list(Num/100, [{decimals, 2}]),
                     Str = "2901828377tianle"++erlang:integer_to_list(RoleId)++Amt++Id,
                     Sign = binary_to_list(util:md5(Str)),
                     Json = json:encode([{'paySign', unicode:characters_to_binary(Id)}, {tmpstr, unicode:characters_to_binary(Sign)}]),
                     {ok, Json};
                ?charge_yao_zfb_h5 ->
                    yao_zfb(zfb_h5, Type, Num, Id, util:to_ip_string(Ip));
                ?charge_fengkong_wx ->
                    fengkong(wx, Type, Num, Id, util:to_ip_string(Ip));
                ?charge_fengkong_zfb ->
                    fengkong-zfb, Type, Num, Id, util:to_ip_string(Ip));
                ?charge_yao_yl ->
                    yao_zfb(yl, Type, Num, Id, util:to_ip_string(Ip));
                ?charge_paysapi_wx ->
                    paysapi(ReturnUrl, Id, RoleId, Num, ChargeType);
                ?charge_paysapi_zfb ->
                    paysapi(ReturnUrl, Id, RoleId, Num, ChargeType);
                99 ->
                    {ok, "SUCCESS"}
            end;
        _ ->
            {false, ?error_busy}
    end.
```

do_charge函数属于charge模块，它会根据支付类型的不同调用不同的方式来处理。

注：参数与handle(1122)一致。



```erlang
%% 个人转账支付
paysapi(ReturnUrl, OrderID, OrderUid, Money, Type) ->
    case charge_mgr:get_info() of
        {ok, {Uid, Token}} ->
            IP = sys_env:get_env(ip),
            %%Uid = sys_env:get_env(paysapi_uid),
            %%Token = sys_env:get_env(paysapi_token),
            GoodsName = "buy_bean",
            IsType = case Type of
                ?charge_paysapi_wx ->
                    2;
                ?charge_paysapi_zfb ->
                    1
            end,
            NotifyUrl = lists:concat(["http://", IP, ":", 10000, "/paysapi"]),
            Price = erlang:float_to_list(Money/100,[{decimals, 2}]),
            String = lists:concat([GoodsName, IsType, NotifyUrl, OrderID, OrderUid, Price, ReturnUrl, Token, Uid]),
            Sign = erlang:binary_to_list(util:md5(String)),
            List = [
                {uid, erlang:list_to_binary(Uid)}
                ,{price, erlang:list_to_binary(Price)}
                ,{istype, IsType}
                ,{notify_url, erlang:list_to_binary(NotifyUrl)}
                ,{return_url, erlang:list_to_binary(ReturnUrl)}
                ,{orderid, erlang:list_to_binary(OrderID)}
                ,{orderuid, OrderUid}
                ,{goodsname, erlang:list_to_binary(GoodsName)}
                ,{key, erlang:list_to_binary(Sign)}
            ],
            Json = json:encode([{url, <<"https://pay.bearsoftware.net.cn/">>} | List]),
            {ok, erlang:binary_to_list(Json)};
        _ ->
            {false, ?error_busy}
    end.

```

该函数主要是向payapi官方申请创建充值订单，接着返回支付相关的数据给前端，让玩家支付。





### 支付回调处理

当玩家支付成功后，payapi官方会回调我们指定的URL，URL就是申请支付订单时指定的回调地址，这里处理payapi的回调。

支付回调处理由web目录下的模块负责，我们来分析下这里的实现。

首先是web_mgr.erl模块，负责启动http服务器。

```erlang
start_link() ->
    Port = sys_env:get_env(web_port),
    ?ERR("后台成功监听~w端口", [Port]),
	mochiweb_http:start([{port, Port},{ssl, false},
		{loop, fun dispatch_requests/1}]).
```

http服务器是基于mochiweb框架的，start_link/1函数指定处理请求的回调函数后就开始监听端口，处理http请求。

```erlang
dispatch_requests(Req) ->	
	Path = Req:get(path),
	Socket = Req:get(socket),
	Action = clean_path(Path),
	web:handle(Action, Req, Socket).

clean_path(Path) ->
	case string:str(Path, "?") of
		0 ->
			Path;
		N ->
			string:substr(Path, 1, string:len(Path) - (N + 1))
	end.
```

dispatch_requests/1负责处理http请求，在这里会监听端口，解析url获取要执行的Action（字符串标识），然后处理请求。

我们来看下web:handle/3函数，这里主要看下paysapi支付回调处理。

```erlang
handle("/paysapi", Req, _) ->
  Params = case catch Req:parse_post() of
       _A -> _A
   end,
  case catch web_callback:paysapi(Params) of
      <<"success">> ->
          success(Req, ?SUCCESS);
      _ ->
          fail(Req, ?FAIL)
 end;
```

首先使用Request模块解析post请求参数，接着执行paysapi回调处理。

```erlang
%% 个人二维码支付回调
paysapi(Params) ->
    %% 获取orderid字段
    case lists:keyfind("orderid", 1, Params) of
        {_, OrderID} ->
            %% 获取realprice字段
            case lists:keyfind("realprice", 1, Params) of
                {_, Price} ->
                    %% 将真实金额转换成分
                    Money = case catch erlang:list_to_integer(Price) of
                        R when is_integer(R) ->
                            erlang:trunc(R * 100);
                        _ ->
                            erlang:round(erlang:list_to_float(Price) * 100)
                    end,
                    %% 将金额转换为分
                    case lists:keyfind("price", 1, Params) of
                        {_, Price1} ->
                            Rmb = case catch erlang:list_to_integer(Price1) of
                                R1 when is_integer(R1) ->
                                    erlang:trunc(R1 * 100);
                                _ ->
                                    erlang:round(erlang:list_to_float(Price1) * 100)
                            end,
                            {_, Uid} = lists:keyfind("orderuid", 1, Params),
                            
                            %% 寻找充值订单
                            case catch db:get_row("select role_id, charge_rmb, type from charge_log where id = ?", [OrderID]) of
                                %% 处理充值订单
                                {ok, [RoleID, Rmb, Type]} ->
                                    %%charge_mgr:unluck_id(Uid),
                                    do_pay_charge(OrderID, RoleID, Type, Money);
                                _ ->
                                    <<"fail">>
                            end;
                        _ ->
                            <<"fail">>
                    end;
                _ ->
                    <<"fail">>
            end;
        _ ->
            <<"fail">>
    end.

```

paysapi会获取处理订单需要的字段，然后执行处理成功的业务逻辑。





充值模块分析

do_pay_charge/5会执行充值的业务逻辑。

```erlang
do_pay_charge(Id, RoleID, Type, Money) ->
    %% 判断是否在线
    case role_data:get_online_role(RoleID) of
        %% 在线的处理方式
        {ok, #online_role{pid = Pid}} ->
            %% 判断玩家进程是否存活
            case is_process_alive(Pid) of
                true ->
                    %% 如果存在则异步发起一个充值回调
                    role:apply(async, Pid, {charge, charge_callback, [{Id, Type, Money}]}),
                    <<"success">>;
                _ ->
                    %% 将玩家添加到充值队列中
                    role_charge_mgr:add_charge(RoleID),
                    %% 在ets表中删除数据
                    ets:delete(online_role, RoleID),
                    %% 从dets表中查找玩家
                    Dets = role_data:get_role_dets_name(RoleID),
                    case dets:lookup(Dets, RoleID) of
                        [Role] -> 
                            {ok, Role1} = role_var:update_var(Role),
                            %% 执行充值回调
                            {ok, NewRole} = charge:charge_callback(Role1, {Id, Type, Money}),
                            %% 更新到数据库
                            role_data:save_to_db(NewRole),
                            role:do_change({charge, charge_callback}, NewRole, Role1),
                            %% 从充值队列中删除
                            role_charge_mgr:delete_charge(RoleID),
                            <<"success">>;
                        _ ->
                            role_charge_mgr:delete_charge(RoleID),
                            <<"fail">>
                    end
            end;
        %% 不在线的处理
        _ ->
            role_charge_mgr:add_charge(RoleID),
            Dets = role_data:get_role_dets_name(RoleID),
            case dets:lookup(Dets, RoleID) of
                [Role] -> 
                    {ok, Role1} = role_var:update_var(Role),
                    {ok, NewRole} = charge:charge_callback(Role1, {Id, Type, Money}),

                    %% 
                    role_data:save_to_db(NewRole),
                    role:do_change({charge, charge_callback}, NewRole, Role1),
                    role_charge_mgr:delete_charge(RoleID),
                    <<"success">>;
                _ ->
                    role_charge_mgr:delete_charge(RoleID),
                    <<"fail">>
            end
    end.
```

- Id：订单id
- RoleID：玩家id
- Type：充值类型，有金币、钻石等
- Money：充值金额，单位还未确定。





### role_charge_mgr.erl

role_charge_mgr是玩家充值模块，它是一个gen_server。

role_charge_mgr实现了一个简单充值队列，就是用一个列表来存储玩家id，提供新增删除接口。

```erlang
%% state是一个列表，即gen_server的State
-record(state, {list = []}).

start_link() ->
    gen_server:start_link({local, ?MODULE}, ?MODULE, [], []).


init([]) ->
    process_flag(trap_exit, true),
    State = #state{},
    {ok, State}.

add_charge(RoleID) ->
    ?MODULE ! {add, RoleID}.

delete_charge(RoleID) ->
    ?MODULE ! {delete, RoleID}.

get_list() ->
    gen_server:call(?MODULE, get_list).

is_charge(RoleID) ->
    case catch gen_server:call(?MODULE, get_list) of 
        {ok, List} ->
            lists:member(RoleID, List);
        _ ->
            true
    end.

handle_info({add, RoleID}, State = #state{list = List}) ->
    {noreply, State#state{list = [RoleID | List]}};

handle_info({delete, RoleID}, State = #state{list = List}) ->
	NewList = lists:delete(RoleID, List),
    {noreply, State#state{list = NewList}}.

handle_call(get_list, _From, State = #state{list = List}) ->
    {reply, {ok, List}, State}.
```







### charge.erl

```erlang
%% 充值回调
%% 金币
charge_callback(Role = #role{vip_charge = VipCharge, role_id = RoleID, vip = Vip, charge = Charge, parent_id = ParentId, first_charge = FirstCharge, luck = Luck, luck_num = LuckNum, channel = Channel, charge_reward_tomorrow = Reward}, {Id, Type = ?charge_coin, Num}) ->
    case db:get_one("select status from charge_log where id = ?", [Id]) of
        {ok, ?charge_status_start} ->
            #vip_welfare{charge_gold = Per} = vip:get_vip_welfare(Vip),
            Value1 = trunc(Num * 10000 /100),
            Value2 = trunc(Value1 * Per /100),
            Value3 = case FirstCharge of
                0 -> trunc(Value1/10);
                _ -> 0
            end,
            Value = Value1 + Value3,
            {ok, NewRole} = role_lib:do_add_coin(Role, Value),
            NewVip = vip:get_lev(VipCharge + Num),
            case NewVip =:= Vip of
                true -> ok;
                _ ->
                    sys_conn:pack_send(1124, #m_1124_toc{vip = NewVip})
            end,
            Now = date:unixtime(),
            db:exec("update charge_log set status = ? , call_time = ?,value = ?, send = ? where id = ?", [?charge_status_finish, Now, Value1, Value2 + Value3, Id]),
            case Channel of
                0 -> ok;
                _ ->
                    db:exec("insert into channel_charge_log(channel_id, role_id, charge_rmb, type, time) value(?, ?, ?, ?, ?)", [Channel, RoleID, Num, Type, Now])
            end,
            sys_conn:pack_send(1123, #m_1123_toc{type = Type, list = [#p_assets{type = coin, num = Value}]}),
            friend_mgr:charge(ParentId, Num),
            account_mgr:charge(Type, Num, Value + Value2),
            AllLuck = case setting_mgr:get(1) of
                {ok, 1} -> Luck;
                _ ->
                    trunc(Num/100) + Luck
            end,
            {NewLuck, NewLuckNum} = do_luck_num(AllLuck, LuckNum),
            NewRole1 = NewRole#role{vip_charge = VipCharge + Num, charge = Charge + Num, vip = NewVip, first_charge = 1, luck = NewLuck, luck_num = NewLuckNum, charge_reward_tomorrow = Reward + Value2},
            NewRole2 = role_lib:do_set_sheep(NewRole1),
            {ok, NewRole2};
        _ ->
            {ok, Role}
    end;
```

这是金币的充值处理。参数如下：

- Id：充值订单id

- Type：充值类型

- Num：充值数量

















